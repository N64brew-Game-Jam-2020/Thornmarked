RATE = 32000

NAMES = [
    "01_Surrender_of_Innocence",
    "02_Underground_Tension",
    "03_Progression",
    "04_Under_the_Moon",
    "05_Descent_Into_Madness",
]

[genrule(
    name = name + "_pcm",
    srcs = [name + ".flac"],
    outs = [name + ".aifc"],
    cmd = ("flac --silent --decode $< --output-name $(RULEDIR)/temp.wav; " +
           "sox $(RULEDIR)/temp.wav --bits 16 --rate %d --channels 1 $@" % RATE),
) for name in NAMES]

[genrule(
    name = name + "_adpcm",
    srcs = [name + ".aifc"],
    outs = [name + "_adpcm.aifc"],
    cmd = ("$(execpath //tools/audio/tabledesign) $< >$(RULEDIR)/codebook.txt; " +
           "$(execpath //tools/audio/encode:vadpcm_enc) -c $(RULEDIR)/codebook.txt $< $@; " +
           "rm $(RULEDIR)/codebook.txt"),
    tools = [
        "//tools/audio/encode:vadpcm_enc",
        "//tools/audio/tabledesign",
    ],
) for name in NAMES]

[genrule(
    name = name + "_dec",
    srcs = [name + "_adpcm.aifc"],
    outs = [name + "_dec.wav"],
    cmd = ("$(execpath //tools/audio/encode:vadpcm_dec) $< >$(RULEDIR)/temp.pcm; " +
           "sox --type raw --bits 16 --rate %d --encoding signed -B $(RULEDIR)/temp.pcm $@" % RATE),
    tools = [
        "//tools/audio/encode:vadpcm_dec",
    ],
) for name in NAMES]
